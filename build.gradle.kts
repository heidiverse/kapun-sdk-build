plugins {
	// Kotlin & KMP plugins
	alias(libs.plugins.kotlin.multiplatform) apply false
	alias(libs.plugins.kotlin.parcelize) apply false
	alias(libs.plugins.kotlin.serialization) apply false
	alias(libs.plugins.kotlin.atomicfu) apply false
	alias(libs.plugins.compose.multiplatform) apply false
	alias(libs.plugins.sqldelight) apply false
	alias(libs.plugins.jetbrains.kotlin.jvm) apply false

	// Android specific plugins
	alias(libs.plugins.android.kotlin.multiplatform.library) apply false
	alias(libs.plugins.android.application) apply false
	alias(libs.plugins.compose.compiler) apply false
	alias(libs.plugins.ksp) apply false
	alias(libs.plugins.ktorfit) apply false

	// iOS specific plugins
	alias(libs.plugins.skie) apply false

	// Rust plugins
	alias(libs.plugins.uniffi.plugin) apply false

	// Library publishing plugins
	alias(libs.plugins.vanniktech.publish) apply false
}

allprojects {
	group = "org.kapunsdk"
	version = getProjectVersion()

	// Static frameworks are the safest default for KMP consumers. Set
	// -PiosFrameworkLinkage=dynamic when publishing an opt-in dynamic XCFramework.
	val iosFrameworkLinkage = providers.gradleProperty("iosFrameworkLinkage")
		.orNull
		?.lowercase()
		?: "static"
	check(iosFrameworkLinkage == "static" || iosFrameworkLinkage == "dynamic") {
		"iosFrameworkLinkage must be either 'static' or 'dynamic'"
	}
	extra["kapunIosFrameworkIsStatic"] = iosFrameworkLinkage == "static"
}

private fun getProjectVersion(): String {
	val versionFromGradleProperties = runCatching { property("ARTIFACT_VERSION").toString() }.getOrNull()
	val versionFromWorkflow = runCatching { property("githubRefName").toString().removePrefix("v") }.getOrNull()
	return versionFromWorkflow ?: versionFromGradleProperties ?: "untagged"
}
